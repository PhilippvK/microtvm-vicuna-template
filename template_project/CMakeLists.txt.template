# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.

# SPDX-License-Identifier: Apache-2.0

cmake_minimum_required(VERSION 3.18)
set(CMAKE_CXX_STANDARD 11)


set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_CURRENT_SOURCE_DIR}/cmake)

SET(CMAKE_SYSTEM_NAME Generic)
SET(CMAKE_SYSTEM_PROCESSOR Vicuna)

SET(VICUNA2_BSP_DIR "${CMAKE_CURRENT_LIST_DIR}/vicuna2" CACHE PATH "Path to Vicuna BSP.")

SET(BOOT_SRCS ${VICUNA2_BSP_DIR}/crt0.S ${VICUNA2_BSP_DIR}/lib/vicuna_crt.c)
SET(BSP_SRCS ${VICUNA2_BSP_DIR}/lib/runtime.c ${VICUNA2_BSP_DIR}/lib/uart.c ${VICUNA2_BSP_DIR}/lib/terminate_benchmark.c)

# select the toolchain and corresponding CMAKE_TOOLCHAIN_FILE
SET(TOOLCHAIN
    "gcc"
    CACHE STRING "select from llvm and gcc for the compilation."
)
SET_PROPERTY(CACHE TOOLCHAIN PROPERTY STRINGS gcc llvm)
# variable_watch(LLVM_DIR)
SET(CMAKE_TOOLCHAIN_FILE "cmake/toolchain_${TOOLCHAIN}.cmake")

# the following three lines can be deleted because it is repeat in LookupClang.cmake
# they are kept to remind that a copy exists in LookupClang.cmake
SET(LLVM_DIR
    ""
    CACHE PATH "install location for riscv-llvm toolchain"
)

SET(RISCV_ELF_GCC_PREFIX
    ""
    CACHE PATH "install location for riscv-gcc toolchain"
)

SET(RISCV_ELF_GCC_BASENAME
    "riscv32-unknown-elf"
    CACHE STRING "base name of the toolchain executables"
)
SET(RISCV_ARCH
    "rv32imafdc"
    CACHE STRING "march argument to the compiler"
)
SET(RISCV_ABI
    "ilp32d"
    CACHE STRING "mabi argument to the compiler"
)

if("${RISCV_ELF_GCC_PREFIX}" STREQUAL "")
    SET(TC_PREFIX "${RISCV_ELF_GCC_BASENAME}-")
else()
    SET(TC_PREFIX "${RISCV_ELF_GCC_PREFIX}/bin/${RISCV_ELF_GCC_BASENAME}-")
endif()

project(crt_autogenerated_project C CXX ASM)

ADD_DEFINITIONS(-march=${RISCV_ARCH})
# ADD_DEFINITIONS(-DVICUNA)
ADD_DEFINITIONS(-mabi=${RISCV_ABI})
ADD_LINK_OPTIONS(-march=${RISCV_ARCH})
ADD_LINK_OPTIONS(-mabi=${RISCV_ABI})
IF (VICUNA_CPU_FREQ_HZ)
    ADD_DEFINITIONS(-DVICUNA_CPU_FREQ_HZ=${VICUNA_CPU_FREQ_HZ})
ENDIF()
ADD_DEFINITIONS(-fno-use-cxa-atexit)
ADD_DEFINITIONS(-fno-rtti)
ADD_DEFINITIONS(-fno-exceptions)
ADD_DEFINITIONS(-fno-threadsafe-statics)
ADD_COMPILE_OPTIONS(-g)
message(STATUS "RISCV_ARCH=${RISCV_ARCH}")
message(STATUS "RISCV_ABI=${RISCV_ABI}")

add_executable(main)

set(CRT_LIB_BASE crt/src/runtime/crt)
set(CRT_LIBS microtvm_rpc_server
  microtvm_rpc_common
  aot_executor_module
  aot_executor
  graph_executor_module
  graph_executor
  common
  memory
)

# Build CRT libraries
foreach(crt_lib_name ${CRT_LIBS})
  add_library(${crt_lib_name})
  file(GLOB_RECURSE crt_lib_srcs ${CRT_LIB_BASE}/${crt_lib_name}/*.c ${CRT_LIB_BASE}/${crt_lib_name}/*.cc)
  target_sources(${crt_lib_name} PRIVATE ${crt_lib_srcs})
  target_include_directories(${crt_lib_name} PRIVATE crt_config crt/include ${VICUNA2_BSP_DIR}/lib)
  target_compile_definitions(${crt_lib_name} PRIVATE -DTVM_HOST_USE_GRAPH_EXECUTOR_MODULE)
  target_compile_options(${crt_lib_name} PUBLIC $<$<COMPILE_LANGUAGE:CXX>:-fno-use-cxa-atexit>)
  # TARGET_LINK_LIBRARIES(${crt_lib_name} PRIVATE c semihost gcc stdc++)
  TARGET_LINK_LIBRARIES(${crt_lib_name} PRIVATE vicuna_bsp)
  target_link_libraries(main PRIVATE ${crt_lib_name})
endforeach(crt_lib_name ${CRT_LIBS})

# Build model files
add_library(tvm_model)
file(GLOB_RECURSE tvm_model_srcs model/codegen/host/src/*.c model/codegen/host/lib/*.o)
target_sources(tvm_model PRIVATE ${tvm_model_srcs})
target_include_directories(tvm_model PRIVATE ${CMAKE_SOURCE_DIR}/include ${CMAKE_SOURCE_DIR}/support crt_config crt/include)
target_compile_options(tvm_model PRIVATE -Wno-error=unused-variable -Wno-error=missing-braces -Wno-error=unused-const-variable -Wno-unused-variable)
set_target_properties(tvm_model PROPERTIES LINKER_LANGUAGE C)
target_link_libraries(tvm_model PRIVATE m)
target_link_libraries(tvm_model PRIVATE vicuna_bsp)
target_link_libraries(main PRIVATE tvm_model)

add_library(vicuna_bsp)
target_sources(vicuna_bsp PRIVATE ${BSP_SRCS})
target_include_directories(vicuna_bsp PRIVATE ${VICUNA2_BSP_DIR}/lib/)
target_link_libraries(main PRIVATE vicuna_bsp)

file(GLOB_RECURSE app_srcs src/**.cc)
target_sources(main PRIVATE ${app_srcs} ${cmsis_lib_srcs} ${BOOT_SRCS})
target_compile_definitions(main PRIVATE -DTVM_HOST_USE_GRAPH_EXECUTOR_MODULE)
target_include_directories(main PRIVATE crt_config include ${CMAKE_SOURCE_DIR}/include crt/include)

SET(VICUNA_LDSCRIPT ${VICUNA2_BSP_DIR}/lld_link.ld)
# SET(VICUNA_LDFLAGS "-L${CMAKE_CURRENT_BINARY_DIR}/riscv_crt0 -T ${VICUNA_LDSCRIPT} -nostdlib")
# SET(VICUNA_LDFLAGS "-T ${VICUNA_LDSCRIPT} -nostartfiles -mcmodel=medany -fvisibility=hidden -fno-use-cxa-atexit")
SET(VICUNA_LDFLAGS "-T ${VICUNA_LDSCRIPT} -nostartfiles -mcmodel=medany -fvisibility=hidden -fno-use-cxa-atexit -ffunction-sections -fdata-sections -Xlinker --gc-sections -fno-rtti -fno-threadsafe-statics -fno-exceptions")

SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${VICUNA_LDFLAGS} -march=${RISCV_ARCH} -mabi=${RISCV_ABI} -Xlinker -Map=linker.map -Xlinker --cref")

TARGET_INCLUDE_DIRECTORIES(main PUBLIC ${VICUNA2_BSP_DIR}/lib/)
# TARGET_LINK_OPTIONS(main PRIVATE ${VICUNA_LDFLAGS} -march=${RISCV_ARCH} -mabi=${RISCV_ABI} -Xlinker -Map=linker.map -Xlinker --cref)

add_custom_command(TARGET main POST_BUILD
    COMMAND ${CMAKE_OBJCOPY} -O verilog ${CMAKE_BINARY_DIR}/main ${CMAKE_BINARY_DIR}/main.hex
    COMMENT "Creating hex")
add_custom_command(TARGET main POST_BUILD
    COMMAND ${CMAKE_OBJCOPY} -O binary ${CMAKE_BINARY_DIR}/main ${CMAKE_BINARY_DIR}/main.bin
    COMMENT "Creating binary")
add_custom_command(TARGET main POST_BUILD
    # COMMAND srec_cat ${CMAKE_BINARY_DIR}/main.bin -binary -offset 0x0000 -byte-swap 4 -o ${CMAKE_BINARY_DIR}/main.vmem -vmem
    COMMAND srec_cat ${CMAKE_BINARY_DIR}/main.bin -binary -offset 0x0000 -byte-swap 4 -o ${CMAKE_BINARY_DIR}/main.vmem -vmem
    COMMENT "Creating vmem")
add_custom_command(TARGET main POST_BUILD
    COMMAND realpath ${CMAKE_BINARY_DIR}/main.vmem > ${CMAKE_BINARY_DIR}/main.path
    COMMENT "Creating paths")
